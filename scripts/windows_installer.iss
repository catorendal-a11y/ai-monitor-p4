#ifndef PackageDir
  #error PackageDir must point to the verified portable package
#endif
#ifndef AppVersion
  #error AppVersion must match FW_VERSION
#endif
#ifndef OutputDir
  #error OutputDir is required
#endif

[Setup]
AppId={{873FC919-66BF-49AE-8265-F25894DBE139}
AppName=AI Monitor
AppVersion={#AppVersion}
AppPublisher=AI Monitor contributors
AppPublisherURL=https://github.com/catorendal-a11y/ai-monitor-p4-s3
DefaultDirName={localappdata}\Programs\AI Monitor
DefaultGroupName=AI Monitor
PrivilegesRequired=lowest
ArchitecturesAllowed=x64os
ArchitecturesInstallIn64BitMode=x64os
MinVersion=10.0
DisableProgramGroupPage=yes
DisableDirPage=auto
WizardStyle=modern
SetupIconFile={#PackageDir}\assets\desktop\nova.ico
UninstallDisplayIcon={app}\AI-Monitor.exe
LicenseFile={#PackageDir}\LICENSE
Compression=lzma2/fast
SolidCompression=yes
OutputDir={#OutputDir}
OutputBaseFilename=AI-Monitor-Setup-v{#AppVersion}-windows
CloseApplications=yes
RestartApplications=no
Uninstallable=yes

[Tasks]
Name: desktopicon; Description: "Create a desktop shortcut"; Flags: unchecked

[Files]
Source: "{#PackageDir}\*"; DestDir: "{app}"; Excludes: "tools\aim_host.json,tools\aim_host.status.json,tools\aim_host.log,tools\activity\*"; Flags: ignoreversion recursesubdirs createallsubdirs

[Icons]
Name: "{userprograms}\AI Monitor"; Filename: "{app}\AI-Monitor.exe"; WorkingDir: "{app}"
Name: "{userdesktop}\AI Monitor"; Filename: "{app}\AI-Monitor.exe"; WorkingDir: "{app}"; Tasks: desktopicon

[Run]
Filename: "{app}\AI-Monitor.exe"; Description: "Open AI Monitor setup"; Flags: nowait postinstall skipifsilent
